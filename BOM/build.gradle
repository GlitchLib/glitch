plugins {
    id "ru.vyarus.pom" version "1.3.0"
}

description = "Bills of Materials (BOM) for Glitch packages"

compileJava.enabled = false
javadoc.enabled = false
tasks.withType(Jar) {
    enabled false
}
delombok.enabled = false
test.enabled = false

withPomXml { Node node ->
    node.appendNode("name").value = "Glitch BOM"
    node.remove(node.dependencyManagement) // removes Project Reactor form dependency management
    Node deps = node.appendNode("dependencyManagement").appendNode("dependencies")

    project.rootProject.subprojects.each {
        if (it.name != "BOM" && it.name != "docs") {
            Node dep = deps.appendNode('dependency')
            dep.appendNode('groupId', it.group)
            dep.appendNode('artifactId', it.archivesBaseName)
            dep.appendNode('version', it.version)
        }
    }
}
