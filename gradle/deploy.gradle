import java.text.SimpleDateFormat

apply plugin: "maven-publish"
apply plugin: "com.jfrog.bintray"
apply plugin: "com.jfrog.artifactory"
apply plugin: "com.github.johnrengelman.shadow"

private static def timestamp() {
    SimpleDateFormat sdf = new SimpleDateFormat("MMM dd yyyy HH:mm:ss zzz")
    sdf.setTimeZone(TimeZone.getTimeZone("GMT"))
    return sdf.format(new Date())
}

def bintrayUser = findProperty("bintrayUser").toString() ?: System.getenv("BINTRAY_USER")
def bintrayApiKey = findProperty("bintrayApiKey").toString() ?: System.getenv("BINTRAY_API_KEY")
def sonatypeUser = findProject("sonatypeUser").toString() ?: System.getenv("CENTRAL_USER")
def sonatypePassword = findProject("sonatypePassword").toString() ?: System.getenv("CENTRAL_PASSWORD")

jar {
    manifest {
        attributes([
                'Manifest-Version': '1.0',
                'Created-By': "Gradle ${gradle.gradleVersion} - JDK ${System.getProperty("java.specification.version")} (${System.getProperty("java.version")})",
                'Implementation-Title': group,
                'Implementation-Vendor': rootProject.name,
                'Implementation-Version': version,
                'Implementation-Date': timestamp()
        ])
    }
}

shadowJar{
    configurations = [ project.configurations.compile ]
    manifest = jar.manifest
}

task sourcesJar(type: Jar, dependsOn: classes, description: 'Builds the sourcesJar.', group: 'build') {
    from sourceSets.main.allSource
    classifier = "sources"
}

task javadocJar(type: Jar,
        dependsOn: javadoc,
        description: "Builds the javadocJar.",
        group: "build") {
    from javadoc.destinationDir
    classifier = "javadoc"
}

artifacts {
    archives sourcesJar
    archives javadocJar
    archives shadowJar
}


publishing {
    publications {
        maven(MavenPublication) {
            from components.java
            artifactId archivesBaseName
            artifact sourcesJar
            artifact javadocJar
            artifact shadowJar
        }
    }
}

bintray {
    user = bintrayUser
    key = bintrayApiKey
    publications = [ 'maven' ]
    dryRun = false
    publish = true
    override = false
    pkg {
        userOrg = bintrayUser
        repo = "GlitchLib"
        name = rootProject.name
        desc = rootProject.description
        licenses = [ "LGPL-3.0" ]
        publicDownloadNumbers = true
        version {
            name = rootProject.version
            vcsTag = "v${rootProject.version}"
            released = Calendar.getInstance(TimeZone.getTimeZone("GMT")).time
        }
    }
}

artifactory {
    contextUrl = 'https://oss.jfrog.org/artifactory'
    publish {
        repository {
            repoKey = 'oss-snapshot-local'
            username = bintrayUser
            password = bintrayApiKey
        }
        defaults {
            publications 'maven'
        }
    }
}

task copyDependencies(type: Copy) {
    from(sourceSets.main.compileClasspath)
    into("${rootProject.buildDir}/archives/${archivesBaseName}/libs")
}

task localPublish(type: Copy, group: "publishing", dependsOn: copyDependencies) {
    from("${buildDir}/libs") {
        include("**/*.jar")
    }
    into("${rootProject.buildDir}/archives/${archivesBaseName}")
}

task deployPublication(group: "publishing") {
    dependsOn localPublish
    if (findProperty("project.deploy") == true) {
        dependsOn(rootProject.version.toString().endsWith("-SNAPSHOT")) ? artifactoryPublish : bintrayUpload
    }
}