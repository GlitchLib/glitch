plugins {
    id "net.ltgt.apt" version "0.18"
    id "com.jfrog.bintray" version "1.8.4"
    id "com.jfrog.artifactory" version "4.7.5"
    id "de.lukaskoerfer.gradle.delombok" version "0.2"
    id "io.spring.dependency-management" version "1.0.6.RELEASE"
}

allprojects {
    apply plugin: "io.spring.dependency-management"

    dependencyManagement {
        imports {
            mavenBom "io.projectreactor:reactor-bom:Californium-SR2"
        }
    }

    repositories {
        jcenter()
    }
}

subprojects {
    apply plugin: "java"
    apply plugin: "net.ltgt.apt"
    apply plugin: "maven-publish"
    apply plugin: "net.ltgt.apt-idea"
    apply plugin: "net.ltgt.apt-eclipse"
    apply plugin: "de.lukaskoerfer.gradle.delombok"

    tasks.withType(JavaCompile) {
        options.incremental = true
        options.encoding = "UTF-8"
    }

    if (project.name != "docs") {
        archivesBaseName = "${rootProject.name.toLowerCase()}-${project.name.replace(':', '-')}"
    }
    sourceCompatibility = targetCompatibility = 1.8

    dependencies {
        if (project.name != "BOM" && project.name != "docs") {
            compile "org.slf4j:slf4j-api:1.7.25"

            compileOnly "org.projectlombok:lombok:1.18.4"
            annotationProcessor "org.projectlombok:lombok:1.18.4"

            testCompile "ch.qos.logback:logback-classic:1.2.3"

            testCompile "io.projectreactor:reactor-test"
        }
    }

    apply from: "${rootDir}/gradle/deploy.gradle"

    test {
        useJUnitPlatform()
    }
}

wrapper {
    gradleVersion = "4.10.2"
    distributionType = Wrapper.DistributionType.ALL
}