plugins {
    id "net.ltgt.apt" version "0.18"
    id "com.jfrog.bintray" version "1.8.4"
    id "com.jfrog.artifactory" version "4.7.5"
    id "de.lukaskoerfer.gradle.delombok" version "0.2"
}

allprojects {
    group = ext["project.groupId"]
    version = ext["project.version"]

    repositories {
        jcenter()
    }
}

subprojects {
    apply plugin: "java"
    apply plugin: "jacoco"
    apply plugin: "net.ltgt.apt"
    apply plugin: "maven-publish"
    apply plugin: "net.ltgt.apt-idea"
    apply plugin: "net.ltgt.apt-eclipse"
    apply plugin: "de.lukaskoerfer.gradle.delombok"

    tasks.withType(JavaCompile) {
        options.incremental = true
        options.encoding = "UTF-8"
    }

    tasks.withType(Javadoc) {
        source = delombok
        options {
            encoding = "UTF-8"
        }
    }

    archivesBaseName = "${rootProject.name}-${project.name.replace(':', '-')}".toLowerCase()

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    def immutables = ext."immutables.version"
    def lombok = ext."lombok.version"

    dependencies {
        if (project != project(":bom")) {

            compileOnly "org.immutables:value:$immutables"
            annotationProcessor "org.immutables:value:$immutables"

            compileOnly "org.projectlombok:lombok:$lombok"
            annotationProcessor "org.projectlombok:lombok:$lombok"
        }
    }

    apply from: "${rootDir}/gradle/deploy.gradle"

    test {
        useJUnitPlatform()
    }
}

task codeCoverageReport(type: JacocoReport) {
    executionData fileTree(project.rootDir.absolutePath).include("**/build/jacoco/*.exec")

    subprojects.each {
        sourceSets it.sourceSets.main
    }
    reports {
        html.enabled = true
        xml.enabled = true
        xml.destination file("${buildDir}/reports/jacoco/report.xml")
    }
}

codeCoverageReport.dependsOn(subprojects*.test)

wrapper {
    gradleVersion = "4.10"
    distributionType = Wrapper.DistributionType.ALL
}