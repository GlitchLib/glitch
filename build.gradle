plugins {
    id "net.ltgt.apt" version "0.18"
    id "com.jfrog.bintray" version "1.8.4"
    id "com.jfrog.artifactory" version "4.7.5"
    id "de.lukaskoerfer.gradle.delombok" version "0.2"
    id "com.gorylenko.gradle-git-properties" version "1.5.1"
    id "io.spring.dependency-management" version "1.0.6.RELEASE"
}

def jackson = ext["jackson.version"]

allprojects {
    apply plugin: "java"
    apply plugin: "jacoco"
    apply plugin: "net.ltgt.apt"
    apply plugin: "maven-publish"
    apply plugin: "net.ltgt.apt-idea"
    apply plugin: "net.ltgt.apt-eclipse"
    apply plugin: "de.lukaskoerfer.gradle.delombok"
    apply plugin: "io.spring.dependency-management"

    group = ext["project.publications.groupId"]
    version = ext["project.version"]

    repositories {
        jcenter()
    }

    dependencyManagement {
        imports {
            mavenBom "com.fasterxml.jackson:jackson-bom:$jackson"
        }
    }

    tasks.withType(Javadoc) {
        if (project != rootProject) {
            source = delombok
        }
        options {
            encoding = "UTF-8"
        }
    }
}

subprojects {
    archivesBaseName = "${rootProject.name}-${project.name}".toLowerCase()

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    def slf4j = ext."slf4j.version"
    def guava = ext."guava.version"
    def rxjava = ext."rxjava.version"
    def immutables = ext."immutables.version"
    def lombok = ext."lombok.version"

    dependencies {
        compile "org.slf4j:slf4j-api:$slf4j"
        compile "com.google.guava:guava:$guava"
        compile "io.reactivex.rxjava2:rxjava:$rxjava"
        compile "com.fasterxml.jackson.core:jackson-databind"
        compile "com.fasterxml.jackson.core:jackson-annotations:$jackson"

        compileOnly "org.immutables:value:$immutables"
        annotationProcessor "org.immutables:value:$immutables"

        compileOnly "org.projectlombok:lombok:$lombok"
        annotationProcessor "org.projectlombok:lombok:$lombok"
    }

    apply from: "${rootDir}/gradle/deploy.gradle"

    test {
        useJUnitPlatform()
    }
}

task codeCoverageReport(type: JacocoReport) {
    executionData fileTree(project.rootDir.absolutePath).include("**/build/jacoco/*.exec")

    subprojects.each {
        sourceSets it.sourceSets.main
    }
    reports {
        html.enabled = true
        xml.enabled = true
        xml.destination file("${buildDir}/reports/jacoco/report.xml")
    }
}

task aggregateJavadoc(type: Javadoc,
        description: "Generate javadocs from all child projects as if it was a single project",
        group: JavaBasePlugin.DOCUMENTATION_GROUP
) {
    destinationDir file("${rootProject.buildDir}/docs/javadoc")
    failOnError false
    title = "${rootProject.name} ${rootProject.version} API"

    options {
        windowTitle = "${rootProject.name} ${rootProject.version}"
        addStringOption("Xdoclint:none", "-quiet")
        author = true
    }

    subprojects.each { project ->
        project.tasks.withType(Javadoc.class).each { doc ->
            source += doc.source
            classpath += doc.classpath
            excludes += doc.excludes
            includes += doc.includes
        }
    }
}

task aggregateJavadocJar(type: Jar, dependsOn: aggregateJavadoc) {
    classifier = 'javadoc'
    from aggregateJavadoc.destinationDir
}

publishing {
    publications {
        maven(MavenPublication) {
            artifact aggregateJavadocJar
        }
    }
}

codeCoverageReport.dependsOn(subprojects*.test)

wrapper {
    gradleVersion = "4.10"
    distributionType = Wrapper.DistributionType.ALL
}