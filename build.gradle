plugins {
    id "com.jfrog.bintray" version "1.8.4"
    id "com.jfrog.artifactory" version "4.7.5"
    id "com.github.johnrengelman.shadow" version "2.0.4"
}

allprojects {

    group = "com.github.stachu540"
    version = "0.1.0-SNAPSHOT"

    repositories {
        jcenter()
    }

    tasks.withType(Javadoc) {
        options {
            encoding = "UTF-8"
        }
    }
}

subprojects {
    apply plugin: "java"
    apply plugin: "maven"

    archivesBaseName = "${rootProject.name}-${project.name}".toLowerCase()

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    dependencies {
        compile "org.slf4j:slf4j-api:1.7.25"
        compile "com.google.guava:guava:26.0-jre"
        compile "io.reactivex.rxjava2:rxjava:2.2.1"
        compile "com.fasterxml.jackson.core:jackson-databind:2.9.6"
    }

    apply from: "${rootDir}/gradle/deploy.gradle"
}

task aggregateJavadoc(type: Javadoc,
        description: "Generate javadocs from all child projects as if it was a single project",
        group: JavaBasePlugin.DOCUMENTATION_GROUP
) {
    destinationDir file("${rootProject.buildDir}/docs/javadoc")
    failOnError false
    title = "${rootProject.name} ${rootProject.version} API"

    options {
        windowTitle = "${rootProject.name} ${rootProject.version}"
        addStringOption("Xdoclint:none", "-quiet")
        author = true
    }

    subprojects.each { project ->
        project.tasks.withType(Javadoc.class).each { doc ->
            source += doc.source
            classpath += doc.classpath
            excludes += doc.excludes
            includes += doc.includes
        }
    }
}

wrapper {
    gradleVersion = "4.10"
    distributionType = Wrapper.DistributionType.ALL
}